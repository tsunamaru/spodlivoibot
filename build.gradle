plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'checkstyle'
}
checkstyle {
    toolVersion '9.0.1'
    configFile file("config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:2021.0.0")
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 17
    targetCompatibility = 17
    options.encoding = 'UTF-8'
}

configurations {
    all {
        implementation {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
        testImplementation {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }
}

repositories {
    mavenCentral()
}

jar {
    enabled = false
}

dependencies {

    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.1'

    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-activemq'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation group: 'com.github.xabgesagtx', name: 'telegram-spring-boot-starter', version: '0.26'

    implementation group: 'ws.schild', name: 'jave-core', version: '3.2.0'
    implementation group: 'ws.schild', name: 'jave-nativebin-linux64', version: '3.2.0'

    implementation 'io.github.furstenheim:copy_down:1.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

}

